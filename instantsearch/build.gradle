buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.2"
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    GROUP = 'com.algolia'
    BASENAME = 'instantsearch-android'
    CODENAME = GROUP + ":" + BASENAME
    NAME = 'InstantSearch Android'
    DESC = "A library of widgets and helpers to build instant-search applications on Android."
    GITHUB = "algolia/instantsearch-android"
    WEBSITE = "https://github.com/" + GITHUB
    REPO = WEBSITE + ".git"
    LICENSE = 'MIT'
    LICENSE_URL = "http://www.opensource.org/licenses/mit-license.php"
    VERSION = '0.4.0'
    VERSION_DESC = 'Version Beta ' + VERSION

    VERSION_APPCOMPAT = '24.2.1'
}

group = project.ext.GROUP
version = project.ext.VERSION

android {
    dataBinding {
        enabled true
    }

    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName project.ext.VERSION
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Disable automatic throwing on system method calls.
    // Rationale: We don't want to mock system methods (for example JSONArray.length),
    // but rather ensure we use them correctly.
    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                testLogging {
                    events "skipped", "failed", "standardOut", "standardError"
                    exceptionFormat "full"
                    outputs.upToDateWhen { false }
                }
            }
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.algolia:algoliasearch-android:3.7.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile ('com.github.bumptech.glide:glide:3.7.0') {
        exclude module: 'asm'
    }
    compile ('com.jayway.jsonpath:json-path:2.2.0') {
        exclude module: 'asm'
    }

    compile "com.android.support:appcompat-v7:$VERSION_APPCOMPAT"
    compile "com.android.support:recyclerview-v7:$VERSION_APPCOMPAT"

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.2"
}

task writeNewPom << {
    apply plugin: 'maven'
    description "Generates pom.xml"

    pom {
        project {
            packaging 'aar'
            groupId project.ext.GROUP
            artifactId project.ext.BASENAME

            name project.ext.CODENAME
            description project.ext.DESC
            url project.ext.WEBSITE

            licenses {
                license {
                    name project.ext.LICENSE
                    url project.ext.LICENSE_URL
                }
            }
            developers {
                developer {
                    id "algolia"
                    name "The Algolia Team"
                    email "hey@algolia.com"
                }
            }
            scm {
                connection project.ext.REPO
                developerConnection project.ext.REPO
                url project.ext.WEBSITE

            }
        }
    }.writeTo("${project.buildDir}/${project.ext.BASENAME}-${project.ext.VERSION}.pom")
            .writeTo("${project.buildDir}/publications/BinTrayPublication/pom-default.xml")
}
task sourceJar(type: Jar, dependsOn: writeNewPom) {
    classifier "source"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false //FIXME: Can I include annotation library and algoliasearch?
    //TODO: Check javadoc "errors": are tags/< displayed correctly?
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

//add javadoc/source jar tasks as artifacts
artifacts {
    archives sourceJar
    archives javadocJar
}

publishing.publications {
    BinTrayPublication(MavenPublication) {
        groupId project.ext.GROUP
        artifactId project.ext.BASENAME
        version project.ext.VERSION

        artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
        artifact sourceJar
        artifact javadocJar
        artifact "${project.buildDir}/${project.ext.BASENAME}-${project.ext.VERSION}.pom"
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['BinTrayPublication']
    publish = true // auto publish after upload
    pkg {
        repo = 'maven'
        name = project.ext.CODENAME
        licenses = [project.ext.LICENSE]
        vcsUrl = project.ext.REPO

        githubRepo = project.ext.GITHUB
        githubReleaseNotesFile = 'README.md'

        version {
            name = project.ext.VERSION
            desc = project.ext.VERSION_DESC
            released = new Date()
            vcsTag = project.ext.VERSION

            gpg {
                sign = true
                //DISCUSS: GPG-sign with our own key instead of BinTray's?
                //This would imply uploading a private/public keypair
//                //passphrase = System.env.GPG_PASSPHRASE
            }

            mavenCentralSync {
                sync = true
                user = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
